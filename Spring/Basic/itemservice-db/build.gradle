plugins {
	id 'org.springframework.boot' version '2.6.5'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
}

group = 'codesver'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

ext["hibernate.version"] = "5.6.5.Final"

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// Thymeleaf template engine
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	// Spring boot web
	implementation 'org.springframework.boot:spring-boot-starter-web'
	// JdbcTemplate
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	// MyBatis
	implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.2.0'
	// (Spring Data) JPA
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	// QueryDSL
	implementation 'com.querydsl:querydsl-jpa'
	annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jpa"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"
	// H2 Database
	runtimeOnly 'com.h2database:h2'
	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	// Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	// Using lombok on test
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'
}

tasks.named('test') {
	useJUnitPlatform()
}

// QueryDSL 추가, 자동 생성된 Q-class gradle clean으로 제거
clean {
	delete file('src/main/generated')
}